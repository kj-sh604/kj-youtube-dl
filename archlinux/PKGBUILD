pkgname=kj-youtube-dl-git
pkgver=r11.201f27f
pkgrel=1
pkgdesc="yup, yet another youtube-dl/yt-dlp GUI wrapper"
arch=('x86_64')
url="https://github.com/kj-sh604/kj-youtube-dl"
license=('0BSD')
depends=('gtk3' 'yt-dlp')
makedepends=('git' 'pkgconf')
optdepends=('mpv: streaming support')
# best practice shit right here
provides=('kj-youtube-dl')
conflicts=('kj-youtube-dl')
source=("git+https://github.com/kj-sh604/kj-youtube-dl.git")
sha384sums=('SKIP')

pkgver() {
  cd "${srcdir}/kj-youtube-dl" || return 1
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/kj-youtube-dl" || return 1
  make PREFIX=/usr
}

package() {
  cd "${srcdir}/kj-youtube-dl" || return 1
  install -Dm755 bin/kj-youtube-dl "${pkgdir}/usr/bin/kj-youtube-dl"
  install -Dm644 archlinux/kj-youtube-dl.desktop "${pkgdir}/usr/share/applications/kj-youtube-dl.desktop"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
